plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'bitxon.dropwizard'
version = '1.0-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(":common-api")
    implementation enforcedPlatform("io.dropwizard:dropwizard-bom:4.0.12")
    implementation 'io.dropwizard:dropwizard-core'
    implementation 'io.dropwizard:dropwizard-client'
    implementation 'io.dropwizard:dropwizard-validation'
    implementation 'io.dropwizard:dropwizard-hibernate'
    implementation 'org.mapstruct:mapstruct:1.5.3.Final'

    annotationProcessor 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.3.Final'

    compileOnly 'org.projectlombok:lombok:1.18.24'

    runtimeOnly 'org.postgresql:postgresql:42.7.5'

    testImplementation 'io.dropwizard:dropwizard-testing'
    testImplementation project(":common-wiremock")
    testImplementation 'org.wiremock:wiremock:3.2.0'
    testImplementation 'com.google.code.findbugs:jsr305:3.0.2' //TODO research - dep should be transitive from wiremock
    testImplementation 'org.testcontainers:testcontainers:1.20.4'
    testImplementation 'org.testcontainers:junit-jupiter:1.20.4'
    testImplementation 'org.testcontainers:postgresql:1.20.4'
    testImplementation 'org.assertj:assertj-core:3.27.3'
    testImplementation 'io.rest-assured:rest-assured:5.5.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testImplementation 'org.junit.jupiter:junit-jupiter-params'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

test {
    useJUnitPlatform()
}

mainClassName = 'bitxon.dropwizard.DropwizardApplication'

jar {
    manifest {
        attributes 'Main-Class': mainClassName
        attributes 'Build-Jdk-Spec': sourceCompatibility
        attributes 'Class-Path': sourceSets.main.runtimeClasspath.collect { it.name }.join(' ')
    }
}

shadowJar {
    mergeServiceFiles()
    exclude 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.SF'
}

run {
    args = ['server', 'classpath:config.yml',]
}

