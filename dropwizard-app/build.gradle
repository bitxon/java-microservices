plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'bitxon.dropwizard'
version = '1.0-SNAPSHOT'
sourceCompatibility = '17'

repositories {
    mavenCentral()
}

dependencies {
    implementation project(":common-api")
    implementation enforcedPlatform("io.dropwizard:dropwizard-bom:4.0.0")
    implementation 'io.dropwizard:dropwizard-core'
    implementation 'io.dropwizard:dropwizard-client'
    implementation 'io.dropwizard:dropwizard-validation'
    implementation 'io.dropwizard:dropwizard-hibernate'
    implementation 'org.mapstruct:mapstruct:1.5.3.Final'

    annotationProcessor 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.3.Final'

    compileOnly 'org.projectlombok:lombok:1.18.24'

    runtimeOnly 'org.postgresql:postgresql:42.4.0'

    testImplementation 'io.dropwizard:dropwizard-testing'
    testImplementation project(":common-wiremock")
    // TODO switch to wiremock 3 after https://github.com/wiremock/wiremock/issues/1967
    testImplementation 'com.github.tomakehurst:wiremock-jre8-standalone:2.35.0'
    testImplementation 'org.testcontainers:testcontainers:1.17.6'
    testImplementation 'org.testcontainers:junit-jupiter:1.17.6'
    testImplementation 'org.testcontainers:postgresql:1.17.6'
    testImplementation 'org.assertj:assertj-core:3.24.2'
    testImplementation 'io.rest-assured:rest-assured:5.3.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testImplementation 'org.junit.jupiter:junit-jupiter-params'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

test {
    useJUnitPlatform()
}

mainClassName = 'bitxon.dropwizard.DropwizardApplication'

jar {
    manifest {
        attributes 'Main-Class': mainClassName
        attributes 'Build-Jdk-Spec': sourceCompatibility
        attributes 'Class-Path': sourceSets.main.runtimeClasspath.collect { it.name }.join(' ')
    }
}

shadowJar {
    mergeServiceFiles()
    exclude 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.SF'
}

run {
    args = ['server', 'classpath:config.yml',]
}

