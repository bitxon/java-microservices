plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group = 'bitxon'
version = '1.0-SNAPSHOT'
sourceCompatibility = '17'

repositories {
    mavenCentral()
}

dependencies {
    implementation project(":common-api")
    implementation 'io.dropwizard:dropwizard-core:2.1.1'
    implementation 'io.dropwizard:dropwizard-client:2.1.1'
    implementation 'io.dropwizard:dropwizard-validation:2.1.1'
    implementation 'io.dropwizard:dropwizard-hibernate:2.1.1'
    implementation 'org.mapstruct:mapstruct:1.5.2.Final'

    annotationProcessor 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.2.Final'

    compileOnly 'org.projectlombok:lombok:1.18.24'

    runtimeOnly 'org.postgresql:postgresql:42.4.0'

    testImplementation 'io.dropwizard:dropwizard-testing:2.1.1'
    testImplementation project(":common-wiremock")
    testImplementation 'com.github.tomakehurst:wiremock-jre8:2.33.2'
    testImplementation 'org.testcontainers:testcontainers:1.17.3'
    testImplementation 'org.testcontainers:junit-jupiter:1.17.3'
    testImplementation 'org.testcontainers:postgresql:1.17.3'
    testImplementation 'org.assertj:assertj-core:3.23.1'
    testImplementation 'io.rest-assured:rest-assured:5.1.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

test {
    useJUnitPlatform()
}

mainClassName = 'bitxon.dropwizard.DropwizardApplication'

jar {
    manifest {
        attributes 'Main-Class': mainClassName
        attributes 'Build-Jdk-Spec': sourceCompatibility
        attributes 'Class-Path': sourceSets.main.runtimeClasspath.collect { it.name }.join(' ')
    }
}

shadowJar {
    mergeServiceFiles()
    exclude 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.SF'
}

run {
    args = ['server', 'classpath:config.yml',]
}

